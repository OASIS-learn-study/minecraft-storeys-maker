buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.0.0'
  }
}

plugins {
    id 'java-library'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'net.ltgt.errorprone' version '0.0.13'
}

repositories {
    // mavenLocal()
    jcenter()
}

allprojects  {
    group = 'ch.vorburger.minecraft.storeys'
    version = '1.0.0-SNAPSHOT'
    description = 'Make your own stories in, with and for Minecraft!'

    apply plugin: 'java-library'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'net.ltgt.errorprone'

    license {
        header rootProject.file('LICENSE-HEADER')
        strictCheck true
    }

    tasks.withType(JavaCompile) {
      // TODO apply same error-prone configuration as I've done in OpenDaylight's infrautils.parent
      options.compilerArgs += [ '-Xlint:deprecation', '-Xep:FallThrough:ERROR', '-Xep:MutableConstantField:ERROR', '-Xep:DefaultCharset:ERROR' ]
    }
}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: 'biz.aQute.bnd.builder'

    repositories {
        mavenLocal() // for ch.vorburger.minecraft.osgi.api
        jcenter()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        errorprone 'com.google.errorprone:error_prone_core:2.3.1'
        implementation 'com.google.errorprone:error_prone_annotations:2.3.1'

        implementation 'org.spongepowered:spongeapi:6.0.0'

        testImplementation 'junit:junit:4.12'
        testImplementation 'com.google.truth:truth:0.30' // NOT 0.31+ which pull 'com.google.guava:guava:20.0', but causes a mess for Gradle's testCompile VS compile; Gradle bug?!
        testImplementation 'org.slf4j:slf4j-simple:1.7.21'
        testImplementation 'org.mockito:mockito-core:2.13.0'
    }

    test {
        testLogging {
            // more verbose, but sometimes useful; see e.g. https://github.com/vorburger/minecraft-storeys-maker/pull/16
            events "failed", "passed", "skipped"
            exceptionFormat "full"
        }
    }
}
