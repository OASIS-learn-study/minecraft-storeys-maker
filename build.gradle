buildscript {
  repositories {
    jcenter()
  }
}

plugins {
    id 'checkstyle'
    id 'java-library'
    id 'com.github.hierynomus.license' version '0.16.1'
    // id 'net.ltgt.errorprone' version '0.0.13'
}

repositories {
    jcenter()
}

allprojects  {
    group = 'ch.vorburger.minecraft.storeys'
    version = '1.0.0-SNAPSHOT'
    description = 'Make your own stories in, with and for Minecraft!'

    apply plugin: 'checkstyle'
    apply plugin: 'java-library'
    apply plugin: 'com.github.hierynomus.license'
    // apply plugin: 'net.ltgt.errorprone'

    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:unchecked"
      // TODO apply same error-prone configuration as I've done in OpenDaylight's infrautils.parent
      // options.compilerArgs += [ '-Xlint:deprecation', '-Xep:FallThrough:ERROR', '-Xep:MutableConstantField:ERROR', '-Xep:DefaultCharset:ERROR' ]
    }
}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }

    license {
        header rootProject.file('LICENSE-HEADER')
        strictCheck true
        excludes(['**/*.json'])
        mapping {
            java='SLASHSTAR_STYLE'
        }
    }

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle:10.1'

        // errorprone 'com.google.errorprone:error_prone_core:2.3.1'
        // implementation 'com.google.errorprone:error_prone_annotations:2.3.1'

        implementation('org.spongepowered:spongeapi:8.1.0') {
            exclude group: 'com.google.guava'
            exclude group: 'com.google.inject'
            exclude group: 'com.google.code.gson'
            exclude group: 'commons-lang3'
        }
        implementation 'com.google.guava:guava:31.1-jre'
        implementation 'com.google.inject:guice:4.2.3'
        implementation 'com.google.code.gson:gson:2.9.0'
        implementation 'org.apache.commons:commons-lang3:3.12.0'

        implementation 'org.slf4j:slf4j-api:1.7.36'
        testImplementation 'org.slf4j:slf4j-simple:1.7.36'

        testImplementation 'junit:junit:4.13.2'
        testImplementation 'org.hamcrest:hamcrest-library:2.2'
        testImplementation 'org.mockito:mockito-core:3.8.0'
    }

    test {
        // https://github.com/OASIS-learn-study/minecraft-storeys-maker/pull/265/
        if (project.hasProperty('excludeTests')) {
            exclude project.property('excludeTests')
        }

        testLogging {
            // more verbose, but sometimes useful; see e.g. https://github.com/vorburger/minecraft-storeys-maker/pull/16
            events "failed", "passed", "skipped"
            exceptionFormat "full"
        }
    }
}
