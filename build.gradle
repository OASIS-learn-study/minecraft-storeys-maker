/*
 * see https://docs.gradle.org/4.1/userguide/java_library_plugin.html
 */

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.5.0'
  }
}

plugins {
    id 'java-library'
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'net.ltgt.errorprone' version '0.0.13'
}

repositories {
    // mavenLocal()
    jcenter()
}

allprojects  {
    group = 'ch.vorburger.minecraft.storeys'
    version = '1.0.0-SNAPSHOT'
    description = 'Make your own stories in, with and for Minecraft!'

    apply plugin: 'java-library'
    // apply plugin: 'org.spongepowered.plugin'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'net.ltgt.errorprone'

    license {
        header rootProject.file('LICENSE-HEADER')
        strictCheck true
    }

    tasks.withType(JavaCompile) {
      // TODO apply same error-prone configuration as I've done in OpenDaylight's infrautils.parent
      options.compilerArgs += [ '-Xlint:deprecation', '-Xep:FallThrough:ERROR', '-Xep:MutableConstantField:ERROR', '-Xep:DefaultCharset:ERROR' ]
    }
}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: 'biz.aQute.bnd.builder'

    repositories {
        mavenLocal() // for ch.vorburger.minecraft.osgi.api
        jcenter()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        implementation 'com.google.errorprone:error_prone_core:2.1.3'
        implementation 'org.spongepowered:spongeapi:6.0.0'

        testImplementation 'junit:junit:4.12'
        testImplementation 'com.google.truth:truth:0.30' // NOT 0.31+ which pull 'com.google.guava:guava:20.0', but causes a mess for Gradle's testCompile VS compile; Gradle bug?!
        testImplementation 'org.slf4j:slf4j-simple:1.7.21'
        testImplementation 'org.mockito:mockito-core:2.13.0'
    }

    test {
        testLogging {
            events "failed" // , "passed", "skipped"
            exceptionFormat "full"
        }
    }
}
