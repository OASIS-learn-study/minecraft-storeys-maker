plugins {
    id "com.github.node-gradle.node" version "3.2.1"
}

project.afterEvaluate {
    tasks.npm_install.outputs.dir(file('node_modules'))
}

node {
    // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md#configuring-the-plugin
    version = '14.15.4'
    npmVersion = '6.14.5'
    download = true
}

task npmInstallDep(type: NpmTask, dependsOn: 'npmSetup') {
    inputs.file('package.json')
    outputs.upToDateWhen { file('node_modules').exists() }

    npmCommand = ['install']
}

task npmBuildProd(type: NpmTask, dependsOn: 'npmInstallDep') {
    inputs.files(fileTree('src'))
    inputs.file('package.json')
    inputs.file('rollup.config.js')

    outputs.dir('dist')

    args = ['run', 'build']
}

task install {
    dependsOn npmBuildProd
}

assemble.dependsOn(install)

apply plugin: 'eclipse'

ext {
  vertxVersion = '4.0.2'
}

dependencies {
    api "io.vertx:vertx-core:$vertxVersion"
    api "io.vertx:vertx-web:$vertxVersion"
    api "io.vertx:vertx-auth-jwt:$vertxVersion"
    api "io.vertx:vertx-service-proxy:$vertxVersion"

    compileOnly "io.vertx:vertx-codegen:$vertxVersion:processor"
    annotationProcessor "io.vertx:vertx-codegen:$vertxVersion:processor"
    annotationProcessor "io.vertx:vertx-service-proxy:$vertxVersion"

    testImplementation "io.vertx:vertx-unit:$vertxVersion"
    testImplementation project(':test-utils')
}

eclipse {
    classpath {
        file.beforeMerged { cp ->
            cp.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/sources/annotationProcessor/java/main', null) )
        }
    }
}
