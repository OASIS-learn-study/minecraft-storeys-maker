plugins {
    id "com.github.node-gradle.node" version "3.0.1"
}

project.afterEvaluate {
    tasks.npm_install.outputs.dir(file('node_modules'))
}

node {
    // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md#configuring-the-plugin
    version = '14.15.4'
    npmVersion = '6.14.5'
    download = true
}

task npmInstallDep(type: NpmTask, dependsOn: 'npmSetup') {
    inputs.file('package.json')
    outputs.upToDateWhen { file('node_modules').exists() }

    npmCommand = ['install']
}

task npmBuildProd(type: NpmTask, dependsOn: 'npmInstallDep') {
    inputs.files(fileTree('src'))
    inputs.file('package.json')
    inputs.file('rollup.config.js')

    outputs.dir('dist')

    args = ['run', 'build']
}

task install {
    dependsOn npmBuildProd
}

assemble.dependsOn(install)

apply plugin: 'eclipse'

ext {
  vertxVersion = '4.0.2'
}

dependencies {
    // just for mapping our enums to catalog types,
    // but this is an implementation detail; users of this API won't directly use spongeapi themselves
    // TODO remove this again, and move enum mapping into impl instead of API...
    implementation 'org.spongepowered:spongeapi:7.3.0'

    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-auth-jwt:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"

    compileOnly "io.vertx:vertx-codegen:$vertxVersion:processor"
    annotationProcessor "io.vertx:vertx-codegen:$vertxVersion:processor"
    annotationProcessor "io.vertx:vertx-service-proxy:$vertxVersion"

    testCompile "io.vertx:vertx-unit:$vertxVersion"
}

eclipse {
    classpath {
        file.beforeMerged { cp ->
            cp.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/sources/annotationProcessor/java/main', null) )
        }
    }
}
