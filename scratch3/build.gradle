plugins {
    id 'org.ajoberstar.grgit' version '4.1.0'
    id "com.github.node-gradle.node" version "3.0.1"
}

dependencies {
    implementation project(':api')
}

node {
    // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md#configuring-the-plugin
    version = '14.15.4'
    npmVersion = '6.14.5'
    download = true
}

['scratch-vm', 'scratch-gui'].collect {
    it
}.eachWithIndex { dir, index ->
    def dest = file("$buildDir/$dir")
    task "cloneScratch${index}" {
        if (!dest.exists()) {
            def repo = grgit.clone(dir: dest, uri: "https://github.com/edewit/${dir}.git")
            repo.checkout {
                branch = 'storeys'
                createBranch = true
                startPoint = 'origin/storeys'
            }
        }
    }

    task "npmDoInstall$index"(type: NpmTask, dependsOn: "cloneScratch$index") {
        workingDir = dest
        args = ['install']
    }

    task "npmBuildProd$index"(type: NpmTask, dependsOn: "npmDoInstall$index") {
        workingDir = dest
        inputs.files(fileTree("${dest}/node_modules"))
        inputs.files(fileTree("${dest}/src"))
        inputs.file("${dest}/package.json")
        inputs.file("${dest}/webpack.config.js")

        outputs.dir("${dest}/dist")

        args = ['run', 'build']
    }
}

task npmBuild {
    dependsOn npmBuildProd0
    dependsOn npmBuildProd1
}

jar.dependsOn(npmBuild)

jar {
    into 'static', {
        from("$buildDir/scratch-gui/build") {
            include '**/*.*'
        }
    }
}
