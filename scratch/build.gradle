plugins {
  id "com.github.node-gradle.node" version "3.0.1"
}

dependencies {
    implementation project(':api')
}

task copyIcon(type: Copy) {
    from 'favicon.ico'
    into 'dist/'
}

node {
  // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md#configuring-the-plugin
    version = '14.15.4'
    npmVersion = '6.14.5'
    download = true
}

task npmInstallDep(type: NpmTask, dependsOn: 'npmSetup') {
    inputs.file('package.json')
    outputs.upToDateWhen { file('node_modules').exists() }

    npmCommand = ['install']
}

task npmBuildProd(type: NpmTask, dependsOn: 'npmInstallDep') {
    inputs.files(fileTree('../api/src'))
    inputs.files(fileTree('src'))
    inputs.file('package.json')
    inputs.file('webpack.config.js')

    outputs.dir('dist')

    args = ['run', 'build:prod']
}

task install {
    dependsOn copyIcon
    dependsOn npmBuildProd
}

assemble.dependsOn(install)

jar {
  into 'static', {
    from('dist') {
      include 'minecraft.scratchx.js'
      include 'favicon.ico'
    }
  }
}
// sourceSets.main.resources.srcDirs = [ "dist/" ]
// sourceSets.main.resources.includes = [ "**/*.js" ]
